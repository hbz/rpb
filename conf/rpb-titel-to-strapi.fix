# Remove titles that are already in lobid-resources
# (see https://jira.hbz-nrw.de/browse/RPB-28)
if exists ("f983")
  if all_match("f36_","s|sm")
    reject()
  end
end
do once()
  put_filemap("https://github.com/hbz/lookup-tables/raw/master/data/almaMmsId2rpbId.tsv",
    "rpbId2almaMmsId", "key_column": "1", "value_column": "0", "sep_char": "\t")
end
paste("inLobid", "~RPB", "f00_", "join_char": "")
lookup("inLobid", "rpbId2almaMmsId", "delete": "true")
if exists("inLobid")
  reject()
end

do put_macro("add")
  if exists("$[from]")
    set_array("_temp")
    copy_field("$[from]", "_temp.$append.f$[to]_")
    add_field("_temp.*.n$[to]a", "$[type]")
    move_field("_temp.*", "f$[to][].$append")
  end
end

do put_macro("add_all")
  set_array("f$[to][]")
  call_macro("add", to: "$[to]", from: "f$[to]?", type: "")
end

call_macro("add_all", to: "24") # Paralleltitel
call_macro("add_all", to: "27") # Abweichende Titel

call_macro("add_all", to: "30") # Sachnotation
call_macro("add_all", to: "31") # Ortsnotation
call_macro("add_all", to: "32") # Schlagwortkette

call_macro("add_all", to: "70") # Quelle

# Seitenzahl in f70s ist Pflicht, in Quelle suchen:
unless exists("f70s")
 put_var("pages", ".*[^\\d](\\d+)\\s*-\\s*(\\d+).*")
 if all_match("f70_", "$[pages]")
  copy_field("f70_", "f70s")
  replace_all("f70s", "$[pages]", "Aus f70_: $1-$2")
 elsif exists("f70_")
  add_field("f70s", "N/A")
 end
end

call_macro("add_all", to: "81") # Anmerkungen
call_macro("add_all", to: "85") # Gesamttitel

# URL
set_array("f90[]")

call_macro("add", to: "90", from: "f90e", type: "Volltext")
call_macro("add", to: "90", from: "f90f", type: "Auszug")

# Person
set_array("f40[]")

call_macro("add", to: "40", from: "f40?", type: "Verfasser")
call_macro("add", to: "40", from: "f41?", type: "Herausgeber")
call_macro("add", to: "40", from: "f42?", type: "Mitwirkender")
call_macro("add", to: "40", from: "f43?", type: "Bearb.")
call_macro("add", to: "40", from: "f44?", type: "Begr.")
call_macro("add", to: "40", from: "f45?", type: "Sammler")
call_macro("add", to: "40", from: "f46?", type: "Librettist")
call_macro("add", to: "40", from: "f47?", type: "Übersetzer")
call_macro("add", to: "40", from: "f50?", type: "Illustrator")
call_macro("add", to: "40", from: "f51?", type: "Verfasser von ergänzendem Text")
call_macro("add", to: "40", from: "f52?", type: "Komponist")
call_macro("add", to: "40", from: "f56?", type: "Red.")
call_macro("add", to: "40", from: "f57?", type: "Sonstige")
call_macro("add", to: "40", from: "f58?", type: "Ausführender")
call_macro("add", to: "40", from: "f59?", type: "Gefeierter")

# Körperschaft
set_array("f60[]")

call_macro("add", to: "60", from: "f60?", type: "HE Urheber")
call_macro("add", to: "60", from: "f61?", type: "NE hrsg. Körperschaft")
call_macro("add", to: "60", from: "f69?", type: "Gefeierte Körperschaft")

vacuum()
