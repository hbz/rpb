# ------- Set JSON-LD context -------

set_field("@context","http://lobid.org/resources/context.jsonld")

# ------- Set "type" -------

set_array("type[]", "BibliographicResource")
if all_equal("#36 ", "u")
  set_field("type[].$append", "Article")
end
if all_equal("#36 ", "s")
  set_field("type[].$append", "Book")
end
if any_equal("#36 ", "sm")
  set_field("type[].$append", "Periodical")
end

# ------- "id", using https URIs -------

move_field("#00 ", "id")
prepend("id","https://lobid.org/resources/RPB") 

# ------- hbzId -------
# Remove titles that are already in lobid-resources
#
# if exists ("#983")
#    reject()
# end

move_field("#983", "hbzId")

# ------- "title" field -------

move_field("#20 ","title")
replace_all("title", "\\s\\+", "")

# ------- "extent" -------

move_field("#77 ", "extent")

# ------- "medium" -------
# Dafür scheint es keine explizite Angabe zu geben.


# -------- "responsibilityStatement" --------
# #39  ist nach Quelldatendoku die "Verfasserangabe (Vorlageform)"

set_array("responsibilityStatement")
move_field("#39 ", "responsibilityStatement[].$append")

# ------- "language" -------
# Dafür scheint es keine explizite Angabe zu geben.

# ------- "publication" object -------

set_array("publication[]")
copy_field("#74 ", "publication[].$append.location")
copy_field("#76a", "publication[].$last.startDate")
replace_all("publication[].$last.startDate","^\\[(\\d{4})\\]","$1")
copy_field("#75 ", "publication[].publishedBy")
set_array("publication[].$last.type[]")
add_field("publication[].$last.type[].$append", "PublicationEvent")

# ------- "zdbId" -------

if any_equal("#36 ", "sm")
  move_field("#88 ", "zdbId")
end

# -------RPB-Sachnotationen --------

set_array("subject[]")

if exists ("#30*")
  move_field("#30*", "subject[].$append.id")
  replace_all("subject[].*.id","_s(n\\d+)_","http://purl.org/lobid/rpb#$1")
end

# To DO: Ergänze source
# "source": {
#    "id": "https://purl.org/lobid/rpb",
#    "label": "Systematik der Rheinland-Pfälzischen Bibliographie"
#    } 

do list(path: "subject[]")
    add_field("source.id", "https://purl.org/lobid/rpb")
    add_field("source.label", "Systematik der Rheinland-Pfälzischen Bibliographie")
end

# ------- Ortsnotation -------

set_array("spatial[]")

if exists ("#31*")
  move_field("#31*", "spatial[].$append.id")
  replace_all("spatial[].*.id","_r99_\\s_o(\\d+)_","rpb-spatial:n$1")
end

# ------- Contribution -------

if exists("#4*")
  set_array("contribution[]")
end

if none_contain("#40 ", "00Gn")
  move_field("#40 ", "contribution[].$append.agent.id")
  replace_all("contribution[].*.agent.id","_(.*)_","https://d-nb.info/gnd/$1")
  do list(path: "contribution[]")
    add_field("role.id", "http://id.loc.gov/vocabulary/relators/aut")
    add_field("role.label", "Autor/in")
  end
  do list(path: "contribution[]")
      set_array("type[]", "Contribution")
  end
end


retain( "type[]", "contribution[]", "extent", "hasItem[]", "responsibilityStatement[]", "language[]", "medium[]", "subject[]", "title", "hbzId", "oclcNumber[]", "otherTitleInformation[]", "natureOfContent[]", "publication[]", "sameAs[]", "describedBy", "@context", "id", "zdbId", "spatial[]" )
vacuum()