# Remove empty fields

vacuum()

# ------- Set JSON-LD context -------

set_field("@context","http://lobid.org/resources/context.jsonld")

# ------- Set "type" -------

set_array("type[]", "BibliographicResource")
if all_equal("#36 ", "u")
  set_field("type[].$append", "Article")
end
if all_equal("#36 ", "s")
  set_field("type[].$append", "Book")
end
if any_equal("#36 ", "sm")
  set_field("type[].$append", "Periodical")
end

# ------- rpbId -------

copy_field("#00 ", "rpbId")
prepend("rpbId","RPB") 

# ------- "id", using https URIs -------

copy_field("rpbId", "id")
prepend("id","https://lobid.org/resources/") 

# ------- hbzId -------
# Remove titles that are already in lobid-resources
#
# if exists ("#983")
#    reject()
# end

move_field("#983", "hbzId")

# ------- "inCollection" -------

set_hash("coll")
set_field("coll.id", "http://lobid.org/resources/HT013494180#!")
set_array("coll.type[]")
add_field("coll.type[].$append", "Collection")
add_field("coll.label", "Rheinland-Pfälzische Bibliographie")
set_array("inCollection[]")
move_field("coll", "inCollection[]")

# ------- "title" field -------

move_field("#20 ","title")
replace_all("title", "\\s\\+", "")

# ------- "extent" -------

move_field("#77 ", "extent")

# ------- "medium" -------
# Dafür scheint es keine explizite Angabe zu geben.


# -------- "responsibilityStatement" --------
# #39  ist nach Quelldatendoku die "Verfasserangabe (Vorlageform)"

set_array("responsibilityStatement")
move_field("#39 ", "responsibilityStatement[]")

# ------- "language" -------
# Dafür scheint es keine explizite Angabe zu geben.

# ------- "publication" object -------

set_hash("pub")
set_array("pub.location[]")
copy_field("#74 ", "pub.location[].$append")
copy_field("#76a", "pub.startDate")
replace_all("pub.startDate","^\\[(\\d{4})\\]","$1")
set_array("pub.publishedBy[]")
copy_field("#75 ", "pub.publishedBy[].$append")
set_array("pub.type[]")
add_field("pub.type[].$append", "PublicationEvent")
set_array("publication[]")
move_field("pub", "publication[]")

# ------- "zdbId" -------

if any_equal("#36 ", "sm")
  move_field("#88 ", "zdbId")
end

# -------RPB-Sachnotationen --------

set_array("subject[]")

if exists ("#30*")
  move_field("#30*", "subject[].$append.id")
  replace_all("subject[].*.id","_s(n\\d+)_","http://purl.org/lobid/rpb#$1")
  do list(path: "subject[]")
    add_field("label", "Platzhalter Schlagwortlabel")
    set_array("type[]")
    add_field("type[].$append", "Concept")
    add_field("source.id", "http://purl.org/lobid/rpb")
    add_field("source.label", "Systematik der Rheinland-Pfälzischen Bibliographie")
  end
end

# ------- Ortsnotation -------
# ToDo: Zu analyiseren: In den Testdaten sind die Werte in "#31 " und "#31a" identisch und ergeben eine Dublette. 

set_array("spatial[]")


move_field("#31*", "spatial[].$append.id")
replace_all("spatial[].*.id","^_r99_\\s_o(\\d+)_$","rpb-spatial:n$1")
replace_all("spatial[].*.id","^_(r\\d{2})_$","rpb-spatial:$1")

do list(path: "spatial[]")
  set_array("type[]")
  add_field("type[].$append", "Concept")
  add_field("source.id", "https://example.org/rpb-spatial")
  add_field("source.label", "Raumsystematik der Rheinland-Pfälzischen Bibliographie")
end

# ------- Contribution -------
# ToDo: 
# 1. Unterscheiden zwischen GND- und Nicht-GND-IDs.
# 2. GND-IDs enthalten ein "n" anstatt einem Minus

if exists("#4*")
  set_array("contribution[]")
end

move_field("#40 ", "contribution[].$append.agent.id")
move_field("#401", "contribution[].$append.agent.id")
move_field("#402", "contribution[].$append.agent.id")
move_field("#403", "contribution[].$append.agent.id")
move_field("#404", "contribution[].$append.agent.id")
replace_all("contribution[].*.agent.id","_(.*)_","https://d-nb.info/gnd/$1")
do list(path: "contribution[]")
  add_field("role.id", "http://id.loc.gov/vocabulary/relators/aut")
  add_field("role.label", "Autor/in")
  add_field("agent.label", "Platzhalter-Autor:in-Name")
  set_array("type[]", "Contribution")
end

retain( "type[]", "contribution[]", "extent", "hasItem[]", "responsibilityStatement[]", "language[]", "medium[]", "subject[]", "title", "hbzId", "oclcNumber[]", "otherTitleInformation[]", "natureOfContent[]", "publication[]", "sameAs[]", "describedBy", "@context", "id", "zdbId", "spatial[]", "inCollection[]", "rpbId" )
vacuum()