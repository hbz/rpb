# Remove empty fields

vacuum()

# ------- Set JSON-LD context -------

set_field("@context","http://lobid.org/resources/context.jsonld")

# ------- Set "type" -------

set_array("type[]", "BibliographicResource")
if all_equal("#36 ", "u")
  set_field("type[].$append", "Article")
end
if all_equal("#36 ", "s")
  set_field("type[].$append", "Book")
end
if any_equal("#36 ", "sm")
  set_field("type[].$append", "Periodical")
end

# ------- rpbId -------

copy_field("#00 ", "rpbId")
prepend("rpbId","RPB") 

# ------- "id", using https URIs -------

copy_field("rpbId", "id")
prepend("id","https://lobid.org/resources/") 

# ------- hbzId -------
# Remove titles that are already in lobid-resources
#
# if exists ("#983")
#    reject()
# end

move_field("#983", "hbzId")

# ------- "inCollection" -------

set_hash("coll")
set_field("coll.id", "http://lobid.org/resources/HT013494180#!")
set_array("coll.type[]")
add_field("coll.type[].$append", "Collection")
add_field("coll.label", "Rheinland-Pfälzische Bibliographie")
set_array("inCollection[]")
move_field("coll", "inCollection[]")

# ------- "title" field -------

move_field("#20 ","title")

# Folgendes rausgenommen wegen 929t124030, 
# wo die sich wiederholenden #20er Probleme machen
# replace_all("title", "\\s\\+", "")

# ------- "extent" -------

move_field("#77 ", "extent")

# ------- "medium" -------
# Dafür scheint es keine explizite Angabe zu geben.


# -------- "responsibilityStatement" --------
# #39  ist nach Quelldatendoku die "Verfasserangabe (Vorlageform)"

set_array("responsibilityStatement")
move_field("#39 ", "responsibilityStatement[]")

# ------- "language" -------
# Dafür scheint es keine explizite Angabe zu geben.

# ------- "publication" object -------

set_hash("pub")
set_array("pub.location[]")
copy_field("#74 ", "pub.location[].$append")
if all_match("#76a", "\\d{4}")
  copy_field("#76a", "pub.startDate")
end

# Folgendes rausgenommen wegen 929t130101, in dem sich #76a wiederholt
# replace_all("pub.startDate","^\\[(\\d{4})\\]","$1")
set_array("pub.publishedBy[]")
copy_field("#75 ", "pub.publishedBy[].$append")
set_array("pub.type[]")
add_field("pub.type[].$append", "PublicationEvent")
set_array("publication[]")
move_field("pub", "publication[]")

# ------- "zdbId" -------

if any_equal("#36 ", "sm")
  move_field("#88 ", "zdbId")
end

# -------RPB-Sachnotationen --------

set_array("subject[]")

if exists ("#30*")
  move_field("#30*", "subject[].$append.id")
  replace_all("subject[].*.id","_s(n\\d+)_","http://purl.org/lobid/rpb#$1")
  do list(path: "subject[]")
    add_field("label", "Platzhalter Schlagwortlabel")
    set_array("type[]")
    add_field("type[].$append", "Concept")
    add_field("source.id", "http://purl.org/lobid/rpb")
    add_field("source.label", "Systematik der Rheinland-Pfälzischen Bibliographie")
  end
end

# ------- Ortsnotation -------
# ToDo: Zu analyiseren: In den Testdaten sind die Werte in "#31 " und "#31a" identisch und ergeben eine Dublette. 

set_array("spatial[]")

# Einige Ortsschlagwörter in "#31~"" stammen aus der GND und werden nach "subject" verschoben

do list (path:"#31?", "var": "$i")
  if any_contain("$i", "_r10_")
    replace_all("$i","^_r10_\\s_(\\d*)n(\\d)_$","https://d-nb.info/gnd/$1-$2")
    move_field("$i", "subject[].$append.id")
  else
    replace_all("$i","^_r99_\\s_o(\\d+)_$","rpb-spatial:n$1")
    replace_all("$i","^_(r\\d{2})_$","rpb-spatial:$1")
    move_field("$i", "spatial[].$append.id")
  end
end

do list(path: "spatial[]")
    set_array("type[]")
    add_field("type[].$append", "Concept")
    add_field("source.id", "https://example.org/rpb-spatial")
    add_field("source.label", "Raumsystematik der Rheinland-Pfälzischen Bibliographie")
end

# ------- Contribution -------

set_array("contribution[]")
copy_field("#40?", "contribution[].$append.agent.id")

do list(path: "contribution[]")
  replace_all("agent.id","_(.*)_","$1")
  copy_field("agent.id", "agent.label")
  lookup("agent.label", "RPB-Export_HBZ_SW.tsv","sep_char":"\t")
  if all_match("agent.id","^00.*|^107n.*|^929n.*")
    prepend("agent.id", "https://rpb.lobid.org/sw/")
  else
    replace_all("agent.id", "(^\\d*)n(\\d)", "$1-$2")
    prepend("agent.id", "https://d-nb.info/gnd/")
  end
  add_field("role.id", "http://id.loc.gov/vocabulary/relators/aut")
  add_field("role.label", "Autor/in")
  set_array("type[]", "Contribution")
end

retain( "type[]", "contribution[]", "extent", "hasItem[]", "responsibilityStatement[]", "language[]", "medium[]", "subject[]", "title", "hbzId", "oclcNumber[]", "otherTitleInformation[]", "natureOfContent[]", "publication[]", "sameAs[]", "describedBy", "@context", "id", "zdbId", "spatial[]", "inCollection[]", "rpbId" )
vacuum()