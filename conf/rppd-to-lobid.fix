do once("map")
  put_filemap("conf/RPB-Export_HBZ_SW.tsv", "SW_map", sep_char: "\t")
end
do once("map_category")
  put_filemap("conf/RPB-Export_HBZ_SWN.tsv", "SWN_map", sep_char: "\t")
end
do once("map_spatial")
  put_rdfmap("https://raw.githubusercontent.com/hbz/lobid-vocabs/master/rpb/rpb-spatial.ttl", "spatial_map", target:"skos:prefLabel", select_language:"de")
end


# ------
#00 RPPD-ID 
# Komentar Doku:  (Achtung: 00 BLANK)

copy_field("f00_","id")
copy_field("f00_","rppdId")
prepend("id","https://rppd.lobid.org/")

# -------
#82b	(GND-ID (R)) ->	gndIdentifier	
# Kommentar Doku:  ohne das vorangestellte Präfix (DE-588)

copy_field("f82b", "gndIdentifier")
unless all_contain("f82b", "Keine GND-Ansetzung")
  copy_field("f82b","id")
  prepend("id","https://d-nb.info/gnd/")
end

set_array("type[]", "AuthorityResource", "Person", "DifferentiatedPerson")

# #1na (Name, bevorzugte Form) -> preferredName
# Name ist aber Kombination aus Geburtsdaten und Name e.g. "f1na": "Marquard, Udo / 1959-"
# Daher müss alles hinter dem Namen ab dem Leerzeichen gelöscht werden

copy_field("f1na", "preferredName")
replace_all("preferredName", "\\/\\s(ca\\.|um)?-?\\s?\\d.+$", "")
trim("preferredName")

# ------
# #1nc (Name Vw-Formen (m)) und #1nd (Andere Namensformen (m)) -> variantName
# In 1nc kann es sein, dass der Name aus #1na erneut wieder vorkommt, das könnte man mit einem Conditional prüfen.
# Kommentar #1nc Doku: sowohl GND, als auch lokale; getrennt mit @; auch bei Doppelnamen vom 2. Teil des Namens; auch Vw-Formen von Pseud/Wirkl. Name;  (wird im web-Katalog nicht angezeigt)
# Kommentar #1nd Doku: sowohl GND, als auch lokale; getrennt mit @, gekennzeichnet mit ¬[Pseud.]¬/¬[Wirkl. Name]¬/¬[Früherer Name]¬/¬[Späterer Name]¬, oder ohne Kennzeichnung bei prägnanter, weiterer Namensform (wird im web-Katalog angezeigt)

set_array("variantName[]")
copy_field("f1nc[].*.f1nc", "variantName[].$append")
copy_field("f1nd[].*.f1nd", "variantName[].$append")
uniq("variantName[]")


# -------
# #1ne (Bilddatei) -> depiction.id, oder url, der thumbnail (neue Property?)
# Komentar Doku: vollständiger Dateiname ohne Pfadangaben, aber inkl. Endung; hinter dem Zeichen "@" kann man eine Quellenangabe einfügen; die zugehörige Datei wird an Herrn Dirx geschickt.; Beispiel: #1ne120251973_1.jpg@Mittelrhein-Museum Koblenz
# Da wir keine Bilder bisher haben, nutze ich hier die Bilder vom GND Explorer.

copy_field("gndIdentifier","@depiction")
prepend("@depiction","https://pictureservice.explore.gnd.network/api/v1/picture/")

set_array("depiction[]")
copy_field("@depiction","depiction[].$append.id")
copy_field("@depiction","depiction[].$last.url")
copy_field("@depiction","depiction[].$last.thumbnail")

# -------
# #1nn (Berufe/Stellungen (m, R)) -> professionOrOccupation
# Kommentar Doku: nach Möglichkeit aus Register 3, sonst Klartext gemäß GND, getrennt mit @
# Hier brauchen wir die Werte aus Register 3, eine Konkoranz auf die GND. Falls nur GND dann nur auf label mappen?
# professionOrOccupationAsLiteral[].*
# professionOrOccupation[].*.id
# professionOrOccupation[].*.label


set_array("professionOrOccupation[]")
do list(path:"f1nn[]", "var": "$i")
  replace_all("$i.f1nn", "_", "")
  copy_field("$i.f1nn", "professionOrOccupation[].$append.label")
  copy_field("$i.f1nn", "professionOrOccupation[].$last.id")
end

do list(path:"professionOrOccupation[]", "var": "$i")
  lookup("$i.label", "SW_map", delete: "true")
  if exists("$i.label")
    if all_match("$i.id", "(^\\d*)n(\\d)")
      replace_all("$i.id", "(^\\d*)n(\\d)", "$1-$2")
      prepend("$i.id", "https://d-nb.info/gnd/")
    else
      prepend("$i.id", "https://rpb.lobid.org/sw/")
    end
  # TODO kein Beispiel in Testdaten
  else
    move_field("$i.id", "$i.label")
  end
end
  

# -------
# #1wz (Wirkungszeitraum) -> periodOfActivity
# Kommentar Doku: Wenn kein Geburtsdatum und kein Todesdatum ermittelbar ist, kann hier ein Zeitraum eingetragen werden
# TODO kein Beispiel in Testdaten
set_array("periodOfActivity[]")
replace_all("f1wz", "(\\d{2})\\.(\\d{2})\\.(\\d{4})", "$3-$2-$1")
copy_field("f1wz", "periodOfActivity[].$append")

# -------
# #1no (Wirkungsort (m, R))-> placeOfActivity
# Kommentar Doku: Bei Orten aus RLP: ID-Nr. aus Register 2, sonst Klartext gem. GND; getrennt mit @, auch Wohnorte; Orte, von denen aus gewirkt wurde, nicht das Verwaltungsgebiet
# Müssen anscheinend die RPB Spatial Ids konstruieren.
# Basis: "spatial_map" ; RPPB-IDs: Unterstriche und vorangestelltes "o" entfernen
# f1no[].*.f1no
# placeOfActivity[].*.id
# placeOfActivity[].*.label

set_array("placeOfActivity[]")
do list(path:"f1no[]", "var": "$i")
  if any_match("$i.f1no", "^_o.+")
    replace_all("$i.f1no", "^_o|_$", "")
    prepend("$i.f1no", "https://rpb.lobid.org/spatial#n")
    copy_field("$i.f1no", "placeOfActivity[].$append.label")
    copy_field("$i.f1no", "placeOfActivity[].$last.id")
    lookup("placeOfActivity[].$last.label", "spatial_map")
  else
    copy_field("$i.f1no", "placeOfActivity[].$append.label")
    prepend("$i.f1no", "https://rpb.lobid.org/dummi/")
    copy_field("$i.f1no", "placeOfActivity[].$last.id")
  end
end


# -------
# #1np (Geburtsdatum) -> dateOfBirth
# Kommentar Doku: mit Punkten und führenden Nullen: 04.05.1950; für Jahrestagsanzeige: die erste 4stellige Zahl im Feld wird als Jahr aufgefasst, dh auch "März 1908" ist möglich; Jahre mit „um“, „vor“, „nach“ und „zwischen“ werden nicht als Jubiläumsjahr aufgefasst.
set_array("dateOfBirth[]")
replace_all("f1np", "(\\d{2})\\.(\\d{2})\\.(\\d{4})", "$3-$2-$1")
copy_field("f1np", "dateOfBirth[].$append")

# -------
# #1nq (Geburtsort (R)) -> placeOfBirth
# Kommentar Doku: Bei Orten aus RLP: ID-Nr. aus Register 2, sonst Klartext gem. GND 

set_array("placeOfBirth[]")
if any_match("f1nq", "^_o.+")
  replace_all("f1nq", "^_o|_$", "")
  prepend("f1nq", "https://rpb.lobid.org/spatial#n")
  copy_field("f1nq", "placeOfBirth[].$append.label")
  copy_field("f1nq", "placeOfBirth[].$last.id")
  lookup("placeOfBirth[].$last.label", "spatial_map")
else
  copy_field("f1nq", "placeOfBirth[].$append.label")
  prepend("f1nq", "https://rpb.lobid.org/dummi/")
  copy_field("f1nq", "placeOfBirth[].$last.id")
end 

# -------
# #1nr (Sterbedatum) -> dateOfDeath
## Kommentar Doku: s. #1np

set_array("dateOfDeath[]")
replace_all("f1nr", "(\\d{2})\\.(\\d{2})\\.(\\d{4})", "$3-$2-$1")
copy_field("f1nr", "dateOfDeath[].$append")

# -------
# #1ns (Sterbeort (R)) -> placeOfDeath
# Kommentar Doku: s. #1nq

set_array("placeOfDeath[]")
if any_match("f1ns", "^_o.+")
  replace_all("f1ns", "^_o|_$", "")
  prepend("f1ns", "https://rpb.lobid.org/spatial#n")
  copy_field("f1ns", "placeOfDeath[].$append.label")
  copy_field("f1ns", "placeOfDeath[].$last.id")
  lookup("placeOfDeath[].$last.label", "spatial_map")
else
  copy_field("f1ns", "placeOfDeath[].$append.label")
  prepend("f1ns", "https://rpb.lobid.org/dummi/")
  copy_field("f1ns", "placeOfDeath[].$last.id")
end 

# -------
# #1nt (persönl. Beziehungen (m, R)) -> relatedPerson (unspezifisch)
# Kommentar Doku: RPPD-ID-Nr. mit Unterstrichen, falls Person bereits in RPPD aufgenommen (Bsp.: _pk0000_), sonst Klartext: Nachname, Vorname / Lebensdaten; getrennt mit @; nähere Ausführungen in 1nz notwendig; verknüpfte Person ist anklickbar 
# TODO: Wenn rppd ID angegeben wird, unterstriche löschen und zu URL transformieren (wie id) und auf Feld relatedPerson[].*.id mappen.

# -------
# #1nt	(persönl. Beziehungen (m, R)) ->	relatedPerson (unspezifisch)	
# Kommentar Doku: RPPD-ID-Nr. mit Unterstrichen, falls Person bereits in RPPD aufgenommen (Bsp.: _pk0000_), sonst Klartext: Nachname, Vorname / Lebensdaten; getrennt mit @; nähere Ausführungen in 1nz notwendig; verknüpfte Person ist anklickbar 

# -------
# #1nu	(Werke (m)) -> 	publication	
# Kommentar Doku: nur Auswahl! Möglichst Originalausgabe angeben. Form:  Titel,  Jahr.  Achtung: Nichtsortierzeichen für führenden Artikel (Alt-170); getrennt mit @; auch Werke von Architekten, Künstlern und Regisseuren

# -------
# #1nv	(Geschlecht) -> 	gender	
# Kommentar Doku: "mmm" (männlich) oder "www" (weiblich) eintragen, 

set_array("gender[]")
if any_equal("f1nv", "mmm")
  set_field("gender[].$append.id", "https://d-nb.info/standards/vocab/gnd/gender#male")
  set_field("gender[].$last.label", "Männlich")
end
if any_equal("f1nv", "www")
  set_field("gender[].$append.id", "https://d-nb.info/standards/vocab/gnd/gender#female")
  set_field("gender[].$last.label", "Weiblich")
end

# -------
# #1nw	(Fachgebiete (m)) -> 	gndSubjectCategory	
# Kommentar Doku:  Normdaten aus SWN-Sätzen (Reg. 9 und Reg. 11), getrennt mit @: Bsp.: #1nw_swn12x2p_@_swn3x6p_; entspricht der GND-Systematik; paarig zu Berufen vergeben
# https://d-nb.info/standards/vocab/gnd/gnd-sc.html#
# gndSubjectCategory[].*.id
# gndSubjectCategory[].*.label

set_array("gndSubjectCategory[]")
do list(path:"f1nw[]", "var": "$i")
  replace_all("$i.f1nw", "_", "")
  replace_all("$i.f1nw", "(\\d)x(\\d)", "$1.$2")
  replace_all("$i.f1nw", "^swn", "")
  copy_field("$i.f1nw", "gndSubjectCategory[].$append.label")
  copy_field("$i.f1nw", "gndSubjectCategory[].$last.id")
end

do list(path:"gndSubjectCategory[]", "var": "$i")
  lookup("$i.label", "SWN_map", delete: "true")
  if exists("$i.label")
    prepend("$i.id", "https://d-nb.info/standards/vocab/gnd/gnd-sc.html#")
  else
    prepend("$i.id", "https://rpb.lobid.org/swn/")
  end
end

# -------
# #1nx	(Quelle (m)) ->	biographicalOrHistoricalInformation	
# Kommentar Doku:  getrennt mit @; Beispiel: Westerwälder Zeitung. - (2002), 224 vom 26.9., S. 20 (evtl. Todesanzeige)@Eigene Angaben (Datum); Genaue Adresse bei Websites: http://www...  (Stand: Datum). Alle Quellen zu einer Person, werden hier zusammengeführt, auch wenn sie aus verschiedenen, eingespielten Werken stammen 

# -------
#1ny	(Datum der letzten inhaltlichen Änderung) ->	describedBy.dateModified	
# Kommentar Doku:  JJJJMMTT, z.B. 20120928 für 28.09.2012

replace_all("f1ny", "(\\d{4})(\\d{2})(\\d{2})", "$1-$2-$3")
copy_field("f1ny", "describedBy.dateModified")

# -------
#1z1	(1. biogr. Anmerkung) ->	biographicalOrHistoricalInformation
# Kommentar Doku:  getrennt durch "; ". Keine Abkürzungen benutzen, da die Stichworte in Register 9 indexiert werden. Zitate aus Quellen in Anführungszeichen; bei sehr langen, biogr. Anmerkungen wird der Text auf mehrere Kategorien aufgeteilt: #1z2, #1z3, #1z4 ... #1z9. Bei eingespielten Biographien werden die Angaben zum Originalwerk am Ende angegeben:  --- [Daten übernommen aus: ....]

vacuum()
retain(
  "dateOfBirth[]",
  "dateOfDeath[]",
  "depiction[]",
  "describedBy",
  "gender[]",
  "gndIdentifier",
  "id",
  "preferredName",
  "professionOrOccupation[]",
  "rppdId", 
  "type[]",
  "variantName[]", 
  "periodOfActivity[]", 
  "placeOfActivity[]",
  "gndSubjectCategory[]", 
  "placeOfBirth[]", 
  "placeOfDeath[]"
  )
	