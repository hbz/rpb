
do put_macro("to_bool")
  if exists("$[field]")
    move_field("$[field]","$[field]~")
    set_field("$[field]~", "true")
  end
end

do put_macro("add")
  if exists("$[from]")
    set_array("_temp")
    copy_field("$[from]", "_temp.$append.$[to]")
    move_field("_temp.*", "$[to][].$append")
    remove_field("$[from]")
  end
end

do put_macro("to_array")
  split_field("$[field]", "@")
  set_array("$[field][]")
  call_macro("add", to: "$[field]", from: "$[field]")
end

do put_macro("add_bio")
  set_array("_temp")
  move_field("$[field]", "_temp.$append") # aufgeteilt: f1?1..9
  join_field("_temp", "\n\n") # als Absätze...
  move_field("_temp", "f1z1[].$append.f1z1") # in 1 Feld
end
do once()
  # map zum Ersetzen der alten GND-ID
  put_filemap("conf/maps/gndId-old-to-new.tsv", "map_gndid_to_new", sep_char: "\t")
end

call_macro("to_bool", field: "f11_")
call_macro("to_bool", field: "f12_")
call_macro("to_bool", field: "f13_")
call_macro("to_bool", field: "f14_")
call_macro("to_bool", field: "f15_")

call_macro("to_array", field: "f1nc")
call_macro("to_array", field: "f1nd")
call_macro("to_array", field: "f1nn")
call_macro("to_array", field: "f1no")
call_macro("to_array", field: "f1nt")
call_macro("to_array", field: "f1nu")
call_macro("to_array", field: "f1nw")
call_macro("to_array", field: "f1nx")

set_array("f1z1[]") # Biogramme
call_macro("add_bio", field: "f1z?")
call_macro("add_bio", field: "f1ü?")
call_macro("add_bio", field: "f1ä?")
call_macro("add_bio", field: "f1ö?")
call_macro("add_bio", field: "f1ß?")

# f82b ist 'required' und 'unique'
if all_match("f82b", "Familienmitglied|Keine GND-Ansetzung")
  paste("f82b", "~Keine GND-Ansetzung für", "f00_")
end
unless exists("f82b")
  paste("f82b", "~Keine GND-Ansetzung für", "f00_")
end
replace_all("f82b", "(\\d+)n(\\d)", "$1-$2")
lookup("f82b", "map_gndid_to_new")

replace_all("f1nv", "w+", "weiblich")
replace_all("f1nv", "m+", "männlich")
replace_all("f1nv", "d+", "divers")

vacuum()
