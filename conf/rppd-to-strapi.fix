
do put_macro("to_bool")
  if exists("$[field]")
    move_field("$[field]","$[field]~")
    set_field("$[field]~", "true")
  end
end

do put_macro("add")
  if exists("$[from]")
    set_array("_temp")
    copy_field("$[from]", "_temp.$append.$[to]")
    move_field("_temp.*", "$[to][].$append")
    remove_field("$[from]")
  end
end

do put_macro("to_array")
  split_field("$[field]", "@")
  set_array("$[field][]")
  call_macro("add", to: "$[field]", from: "$[field]")
end

do put_macro("add_bio")
  set_array("_temp")
  move_field("$[field]", "_temp.$append") # aufgeteilt: f1?1..9
  join_field("_temp", "\n\n") # als Absätze...
  move_field("_temp", "f1z1[].$append.f1z1") # in 1 Feld
end

do put_macro("place_to_uri")
  if any_match("$[field]", "^_o.+")
    replace_all("$[field]", "^_o|_$", "")
    prepend("$[field]", "https://rpb.lobid.org/spatial#n")
  end
end

do once()
  # map zum Ersetzen der alten GND-ID
  put_filemap("conf/maps/gndId-old-to-new.tsv", "map_gndid_to_new", sep_char: "\t")
  put_filemap("conf/maps/gndId-to-rppdId.tsv", "map_rel_gndId",key_column:"0",value_column:"1", sep_char: "\t", expected_columns:"-1")
  put_filemap("conf/maps/rppdId-with-label.tsv", "map_rel_rppdId",key_column:"0",value_column:"1", sep_char: "\t", expected_columns:"-1")  
end

move_field("f00_","rppdId")
move_field("f82b", "gndIdentifier")

call_macro("to_bool", field: "f11_")
call_macro("to_bool", field: "f12_")
call_macro("to_bool", field: "f13_")
call_macro("to_bool", field: "f14_")
call_macro("to_bool", field: "f15_")

call_macro("to_array", field: "f1nc")
call_macro("to_array", field: "f1nd")
call_macro("to_array", field: "f1nn")
call_macro("to_array", field: "f1no")
call_macro("to_array", field: "f1nt")
call_macro("to_array", field: "f1nu")
call_macro("to_array", field: "f1nw")
call_macro("to_array", field: "f1nx")

set_array("f1z1[]") # Biogramme
call_macro("add_bio", field: "f1z?")
call_macro("add_bio", field: "f1ü?")
call_macro("add_bio", field: "f1ä?")
call_macro("add_bio", field: "f1ö?")
call_macro("add_bio", field: "f1ß?")

# gndIdentifier ist 'required' und 'unique'
if all_match("gndIdentifier", "Familienmitglied|Keine GND-Ansetzung")
  paste("gndIdentifier", "~Keine GND-Ansetzung für", "rppdId")
end
unless exists("gndIdentifier")
  paste("gndIdentifier", "~Keine GND-Ansetzung für", "rppdId")
end
replace_all("gndIdentifier", "(\\d+)n(\\d)", "$1-$2")
lookup("gndIdentifier", "map_gndid_to_new")

replace_all("f1nv", "w+", "weiblich")
replace_all("f1nv", "m+", "männlich")
replace_all("f1nv", "d+", "divers")

# URIs / Lookup-Felder

# Geburts-, Sterbe- und Wirkungsorte
call_macro("place_to_uri", field: "f1nq") 
call_macro("place_to_uri", field: "f1ns")
do list_as(f: "f1no[].*")
  call_macro("place_to_uri", field: "f.f1no")
end

# Berufe
do list(path:"f1nn[]", "var": "$i")
  replace_all("$i.f1nn", "_", "")
  if all_match("$i.f1nn", "(^\\d*)n(\\d)")
    replace_all("$i.f1nn", "(^\\d*)n(\\d)", "$1-$2")
    prepend("$i.f1nn", "https://d-nb.info/gnd/")
  else
    prepend("$i.f1nn", "https://rpb.lobid.org/sw/")
  end
end

# Fachgebiete
do list(path:"f1nw[]", "var": "$i")
  replace_all("$i.f1nw", "_", "")
  replace_all("$i.f1nw", "(\\d)x(\\d)", "$1.$2")
  replace_all("$i.f1nw", "^swn", "")
  prepend("$i.f1nw", "https://w3id.org/lobid/rpb-fachgebiete/n")
end

# Beziehungen
do list(path:"f1nt[]", "var": "$i")
  if any_match("$i.f1nt", "^_.+")
    replace_all("$i.f1nt", "_", "")
    lookup("$i.f1nt", "map_rel_gndId")
    lookup("$i.f1nt", "map_rel_rppdId")
  end
end

vacuum()
